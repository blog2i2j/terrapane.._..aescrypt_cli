# This file facilitates creation of package files for OS installation
if(aescrypt_cli_INSTALL)
    if(LINUX OR APPLE OR WIN32)
        include(InstallRequiredSystemLibraries)
        set(CPACK_PACKAGE_NAME "aescrypt_cli")
        set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "${PROJECT_DESCRIPTION}")
        set(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/description.txt")
        set(CPACK_PACKAGE_VENDOR "Terrapane Group, Inc.")
        set(CPACK_PACKAGE_CONTACT "support@terrapane.com")
        set(CPACK_RESOURCE_FILE_LICENSE "${PROJECT_SOURCE_DIR}/LICENSE.md")
        set(CPACK_PACKAGE_VERSION_MAJOR "${PROJECT_VERSION_MAJOR}")
        set(CPACK_PACKAGE_VERSION_MINOR "${PROJECT_VERSION_MINOR}")
        set(CPACK_PACKAGE_VERSION_PATCH "${PROJECT_VERSION_PATCH}")
        set(CPACK_PACKAGE_VERSION_TWEAK "${PROJECT_VERSION_TWEAK}")
        set(CPACK_PACKAGE_VERSION "${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH}")
        if(DEFINED PROJECT_VERSION_TWEAK AND PROJECT_VERSION_TWEAK GREATER 0)
            set(CPACK_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION}.${PROJECT_VERSION_TWEAK}")
        endif()
        set(CPACK_VERBATIM_VARIABLES TRUE)
        if(APPLE AND CMAKE_OSX_ARCHITECTURES)
            set(CPACK_PACKAGE_FILE_NAME ${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-${CMAKE_SYSTEM_NAME}-${CMAKE_OSX_ARCHITECTURES})
        else()
            if(CMAKE_SYSTEM_PROCESSOR STREQUAL "AMD64")
                set(AESCRYPT_SYSTEM_PROCESSOR "x86_64")
            else()
                set(AESCRYPT_SYSTEM_PROCESSOR ${CMAKE_SYSTEM_PROCESSOR})
            endif()
            set(CPACK_PACKAGE_FILE_NAME ${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-${CMAKE_SYSTEM_NAME}-${AESCRYPT_SYSTEM_PROCESSOR})
        endif()
        if(LINUX)
            set(CPACK_RPM_PACKAGE_RELEASE 1)
            set(CPACK_RPM_PACKAGE_GROUP "Unspecified")
            set(CPACK_RPM_PACKAGE_LICENSE "Commercial; See https://www.aescrypt.com/license.html")
            set(CPACK_RPM_PACKAGE_REQUIRES "glibc, libstdc++")
            set(CPACK_DEBIAN_PACKAGE_SECTION "utils")
            set(CPACK_DEBIAN_PACKAGE_LICENSE "Commercial; See https://www.aescrypt.com/license.html")
            set(CPACK_DEBIAN_PACKAGE_DEPENDS "libc6, libstdc++6")
            set(CPACK_GENERATOR RPM DEB TGZ)
        elseif(WIN32)
            set(CPACK_GENERATOR ZIP)
        else()
            set(CPACK_GENERATOR TGZ)
        endif()
        include(CPack)
    endif()
endif()
